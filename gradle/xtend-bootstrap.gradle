repositories {
	jcenter()
}

configurations {
	xtendCompiler {
		description 'Bootstrap dependencies for the Xtend compiler'
		resolutionStrategy {
			eachDependency {
				if (requested.group == 'org.eclipse.xtext' || requested.group == 'org.eclipse.xtend')
					useVersion(bootstrapXtextVersion)
				if (requested.group == 'com.google.inject' && requested.name == 'guice')
					useVersion('3.0')
			}
		}
		exclude group: 'asm'
	}
}

dependencies {
	xtendCompiler "org.eclipse.xtend:org.eclipse.xtend.core"
	xtendCompiler "org.xtext:xtext-gradle-builder:2.0.2"
}

subprojects {
	plugins.withId('org.xtext.xtend') {
	  dependencies {
	    compile "org.eclipse.xtend:org.eclipse.xtend.lib:$minimumXtextVersion"
	  }

	  tasks.matching {it.class.simpleName.startsWith('XtextGenerate')}.all {
			afterEvaluate {
				xtextClasspath = rootProject.configurations.xtendCompiler
			}
	  }
	}
}
